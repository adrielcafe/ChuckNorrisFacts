apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "cafe.adriel.chucknorrisfacts"

        minSdkVersion 21
        targetSdkVersion 28

        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'CHUCK_NORRIS_API_BASE_URL', '\"https://api.chucknorris.io/jokes/\"'
    }
    buildTypes {
        debug {
            useProguard false
            buildConfigField 'Boolean', 'RELEASE', 'false'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'Boolean', 'RELEASE', 'true'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += ['src/test/kotlin', 'src/sharedTest/kotlin']
        test.resources.srcDirs += 'src/sharedTest/resources'
        androidTest.java.srcDirs += ['src/androidTest/kotlin', 'src/sharedTest/kotlin']
        androidTest.resources.srcDirs += 'src/sharedTest/resources'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    dexOptions {
        preDexLibraries true
        maxProcessCount 8
    }
    androidExtensions {
        experimental true
        defaultCacheImplementation "SPARSE_ARRAY"
    }
}

kapt {
    useBuildCache true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Android
    implementation "com.google.android.material:material:1.1.0-alpha04"
    implementation "androidx.appcompat:appcompat:1.1.0-alpha03"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha3"
    implementation "androidx.lifecycle:lifecycle-extensions:2.1.0-alpha03"
    implementation "androidx.core:core-ktx:1.1.0-alpha05"
    implementation "androidx.fragment:fragment-ktx:1.1.0-alpha05"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:2.2.7"
    implementation "io.reactivex.rxjava2:rxkotlin:2.3.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "com.github.pakoito:RxPaper2:1.4.0"

    // Network
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.okhttp3:logging-interceptor:3.14.0"

    // DI
    implementation "org.koin:koin-android-viewmodel:2.0.0-beta-5"
    
    // Immutable State
    implementation "com.github.etiennelenhart:eiffel:4.1.0"

    // View
    implementation "cz.kinst.jakub:android-stateful-layout-base:2.0.8"
    implementation "pl.droidsonroids.gif:android-gif-drawable:1.2.16"

    // Util
    implementation "com.github.ajalt:timberkt:1.5.1"
    implementation "com.link184:kid-adapter:1.1.7"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$leakcanary_version"

    // Unit Test
    testImplementation "junit:junit:4.12"

    // Espresso Test
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.1"
    androidTestImplementation "androidx.test:core:1.1.0"
    androidTestImplementation "androidx.test:core-ktx:1.1.0"
    androidTestImplementation "androidx.test:runner:1.1.1"
    androidTestImplementation "androidx.test.ext:junit:1.1.0"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.0"
    androidTestImplementation "com.github.adrielcafe:Barista:assertListHasItems-SNAPSHOT" // Fork https://github.com/adrielcafe/Barista/tree/assertListHasItems
    androidTestImplementation "com.squareup.rx.idler:rx2-idler:0.9.1"
    androidTestImplementation "com.github.gmazzo:okhttp-mock:1.3.1"
}

apply from: '../spotless.gradle'