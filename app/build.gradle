apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'jacoco-android'

// Robolectric and Paper use different versions of ASM lib, so we must force the most recent one
configurations.all {
    resolutionStrategy.force 'org.ow2.asm:asm:7.1'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "cafe.adriel.chucknorrisfacts"

        minSdkVersion 21
        targetSdkVersion 28

        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "cafe.adriel.chucknorrisfacts.runner.TestRunner"

        buildConfigField 'String', 'CHUCK_NORRIS_API_BASE_URL', '\"https://api.chucknorris.io/jokes/\"'
    }
    buildTypes {
        debug {
            useProguard false
            testCoverageEnabled true
            buildConfigField 'Boolean', 'RELEASE', 'false'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'Boolean', 'RELEASE', 'true'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'

        test.java.srcDirs += ['src/test/kotlin', 'src/sharedTest/kotlin']
        test.resources.srcDirs += 'src/sharedTest/resources'

        androidTest.java.srcDirs += ['src/androidTest/kotlin', 'src/sharedTest/kotlin']
        androidTest.resources.srcDirs += 'src/sharedTest/resources'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    dexOptions {
        preDexLibraries true
        maxProcessCount 8
    }
    androidExtensions {
        experimental true
        defaultCacheImplementation "SPARSE_ARRAY"
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true
        unitTests {
            includeAndroidResources true
        }
    }
}

kapt {
    useBuildCache true
}

jacoco {
    toolVersion "0.8.3"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Android
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "com.github.pakoito:RxPaper2:$rxpaper2_version"

    // Network
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$logging_interceptor_version"

    // View
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.link184:kid-adapter:$kid_adapter_version"
    implementation "cz.kinst.jakub:android-stateful-layout-base:$stateful_layout_version"

    // Util
    implementation "org.koin:koin-android-viewmodel:$koin_version"
    implementation "com.github.etiennelenhart:eiffel:$eiffel_version"
    implementation "com.github.ajalt:timberkt:$timberkt_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$leakcanary_version"

    // Unit Test
    testImplementation "junit:junit:$test_junit_version"
    testImplementation "org.robolectric:robolectric:$test_robolectric_version"
    testImplementation "androidx.test.espresso:espresso-core:$test_espresso_version"
    testImplementation "androidx.test.espresso:espresso-intents:$test_espresso_version"
    testImplementation "androidx.test:core-ktx:$test_core_ktx_version"
    testImplementation "androidx.test.ext:junit-ktx:$test_junit_ktx_version"
    testImplementation "org.koin:koin-test:$koin_version"
    testImplementation "com.github.robfletcher.strikt:strikt-core:$test_strikt_version"
    testImplementation "org.opentest4j:opentest4j:$test_opentest4j_version"
    testImplementation "com.github.adrielcafe:Barista:$test_barista_version"
    testImplementation "com.squareup.rx.idler:rx2-idler:$test_rx2_idler_version"
    testImplementation "com.github.gmazzo:okhttp-mock:$test_okhttp_mock_version"

    // Espresso Test
    androidTestUtil "androidx.test:orchestrator:$test_orchestrator_version"
    androidTestImplementation "androidx.test:runner:$test_runner_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$test_espresso_version"
    androidTestImplementation "androidx.test:core-ktx:$test_core_ktx_version"
    androidTestImplementation "androidx.test.ext:junit-ktx:$test_junit_ktx_version"
    androidTestImplementation "com.github.adrielcafe:Barista:$test_barista_version"
    androidTestImplementation "com.squareup.rx.idler:rx2-idler:$test_rx2_idler_version"
    androidTestImplementation "com.github.gmazzo:okhttp-mock:$test_okhttp_mock_version"
}

apply from: '../spotless.gradle'